plugins {
    id 'groovy'
    id 'codenarc'
    id 'net.saliman.cobertura' version '2.5.4'
    id 'idea'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.6.0'
    id "com.jfrog.bintray" version "1.8.0"
    id 'nebula.integtest' version '3.4.1'
    id 'com.gradle.plugin-publish' version '0.9.10'
    id 'com.github.johnrengelman.plugin-shadow' version '2.0.3'
    id 'com.github.ben-manes.versions' version '0.17.0'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7


repositories {
    maven {
        url "http://repo.bodar.com"
    }
    jcenter()
}

/*
 * Gradle leaks dependencies into the plugin. Sometimes the plugin gets an incompatible version.
 * For example, with Gradle 4, commons-io is a 1.x version, whereas this plugin needs 2.x. The
 * shadow plugin copies dependencies into the JAR file and repackages them to avoid this problem.
 * We don't want to do all of the dependencies. Some do not work when shadowed, and it makes the
 * plugin JAR large. The following list are dependencies to exclude from shadowing. They must
 * include all dependencies that are excluded by 'shadowJar.dependencies.exclude'. We cannot use
 * this list in the 'exclude' calls, because 'shadowJar.dependencies.exclude' needs to consider
 * transitive dependencies, the follow list should not include transitive.
 */
def shadowExcludeDependencies = [
    'org.codehaus.gpars:gpars:1.2.1',
    'org.zeroturnaround:zt-exec:1.10',
    'org.apache.logging.log4j:log4j-core:2.8.2',
    'org.apache.logging.log4j:log4j-api:2.8.2',
    'com.fasterxml.jackson.core:jackson-databind:2.8.6',
    'org.apache.velocity:velocity:1.7',
    'velocity-tools:velocity-tools:1.4',
    'joda-time:joda-time:2.9.9',
    'net.lingala.zip4j:zip4j:1.3.2',
    'org.codehaus.plexus:plexus-utils:3.0.24',
    'org.jsoup:jsoup:1.10.3',
    'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20170515.1',
    'com.google.guava:guava:20.0'
]

dependencies {
    shadow gradleApi()

    compile('net.masterthought:cucumber-reporting:3.16.0') {
        shadowExcludeDependencies.each {
            def split = it.split(':')
            exclude(group: split[0], module: split[1])
        }
    }
    shadow(shadowExcludeDependencies)

    //testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile gradleApi()
    testCompile shadowExcludeDependencies
    testCompile "org.codehaus.groovy:groovy-all:2.4.12"
    testCompile 'com.netflix.nebula:nebula-test:6.3.0'

    //intTestCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    integTestCompile gradleApi()
    integTestCompile shadowExcludeDependencies
    integTestCompile 'com.netflix.nebula:nebula-test:6.3.0'
}

configureRelocationShadowJar {
    prefix = 'com.commercehub.gradle.cucumber.shadow'
}

shadowJar {
    dependencies {
        exclude(dependency {
            // net.masterthought:cucumber-reporting MUST be shadowed so it will reference the shadowed JARs.
            // We'll shadow all commons dependencies, they are likely to be used in Gradle.
            !it.moduleGroup.contains('commons') && it.moduleGroup != 'net.masterthought'
        })
    }
}

codenarc {
    configFile = rootProject.file('codenarcrule.groovy')
    reportFormat = System.properties['codeNarcOutput'] ?: 'html'
    maxPriority2Violations = Integer.MAX_VALUE
    maxPriority3Violations = Integer.MAX_VALUE
}

cobertura {
    coverageIgnoreTrivial = true
    auxiliaryClasspath += project.sourceSets.main.runtimeClasspath
}

test {
    finalizedBy coberturaReport
}

integrationTest {
    finalizedBy coberturaReport
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            project.shadow.component(publication)

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

bintray {
    user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
    key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
    publications = ["mavenJava"]
    pkg {
        repo = project.hasProperty("bintrayRepo") ? bintrayRepo : "main"
        userOrg = project.hasProperty("bintrayUserOrg") ? bintrayUserOrg : "commercehub-oss"
        name = 'gradle-cucumber-jvm-plugin'
        licenses = ["Apache-2.0"]
        vcsUrl = 'https://github.com/commercehub-oss/gradle-cucumber-jvm-plugin'
    }
}

jar {
    enabled = false
    dependsOn(shadowJar { classifier = null })
}

bintrayUpload.dependsOn build, sourcesJar, javadocJar
createReleaseTag.dependsOn bintrayUpload
createReleaseTag.dependsOn publishPlugins

pluginBundle {
    website = "https://github.com/commercehub-oss/gradle-cucumber-jvm-plugin"
    vcsUrl = "https://github.com/commercehub-oss/gradle-cucumber-jvm-plugin"
    description = "The gradle cucumber-jvm plugin provides the ability to run cucumber acceptance tests directly from a gradle build."
    tags = ["cucumber", "java", "testing"]
    plugins {
        cucumberJvm {
            id = "com.commercehub.cucumber-jvm"
            displayName = "Cucumber-JVM Plugin"
        }
    }
    mavenCoordinates {
        groupId = "com.commercehub"
        artifactId = "gradle-cucumber-jvm-plugin"
    }
}
